buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.javamodularity:moduleplugin:1.6.0"
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

def javamodularityPluginId = 'org.javamodularity.moduleplugin'
final hasPlugin = project.getPlugins().hasPlugin(javamodularityPluginId);
if(hasPlugin) {
    final Plugin plugin = project.getPlugins().getPlugin(javamodularityPluginId)
    println 'Plugin already applied - version ' + plugin.properties['javamodularityPluginId']
} else {
    apply plugin: "org.javamodularity.moduleplugin"
}

group 'com.craigraw'
version '0.1'

sourceCompatibility = 1.9
targetCompatibility = 1.9

repositories {
    mavenCentral()
}

dependencies {
    implementation ('org.zeromq:jeromq:0.5.0') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    implementation ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
        exclude group: 'junit', module: 'junit'
    }
    implementation ('org.bouncycastle:bcprov-jdk15on:1.64') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    implementation ('ch.qos.logback:logback-classic:1.2.3') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation ('junit:junit:4.12') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: '2.2'
}

task(runDrongo, dependsOn: 'classes', type: JavaExec) {
    main = 'com.craigraw.drongo.Main'
    classpath = sourceSets.main.runtimeClasspath
    args 'drongo.properties'
}

jar {
    manifest {
        attributes "Main-Class": "com.craigraw.drongo.Main"
    }

    baseName = 'drongo'
    version = '0.1'
}

shadowJar {
    version = '0.1'
    classifier = 'all'
}
